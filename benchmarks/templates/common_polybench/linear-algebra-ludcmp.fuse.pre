
decl A_int: ubit<32>[N][N];
decl b_int: ubit<32>[N];
decl x_int: ubit<32>[N];
decl y_int: ubit<32>[N];

// outer 1
for (let i: N_bw = 0..N) {
  // inner 1.1
  let j: N_bw = 0;
  while (j < i) {
    decor "#pragma HLS loop_tripcount WHILE1_1"
    let w: ubit<32> = A_int[i][j];
    ---
    // inner 1.1.1
    let k: N_bw = 0;
    while (k < j) {
      decor "#pragma HLS loop_tripcount WHILE1_1_1"
      let A_i_k: ubit<32> = A_int[i][k];
      ---
      w := w - (A_i_k * A_int[k][j]);
      k := k + 1;
    }
    ---
    let A_j_j: ubit<32> = A_int[j][j];
    ---
    if (A_j_j != 0) {
      A_int[i][j] := w / A_j_j;
    }
    j := j + 1;
  }

  ---

  // inner 1.2
  j := i;
  while(j < N) {
    decor "#pragma HLS loop_tripcount WHILE1_2"
    let w: ubit<32> = A_int[i][j];
    ---
    // inner 1.2.1
    let k: N_bw = 0;
    while(k < i) {
      decor "#pragma HLS loop_tripcount WHILE1_2_1"
      let A_i_k: ubit<32> = A_int[i][k];
      ---
      w := w - (A_i_k * A_int[k][j]);
      k := k + 1;
    }
    ---
    A_int[i][j] := w;

    j := j + 1;
  }
}

---

// outer 2
for (let i: N_bw = 0..N) {
  let w: ubit<32> = b_int[i];
  // inner 2.1
  let j: N_bw = 0;
  while (j < i) {
    decor "#pragma HLS loop_tripcount WHILE2_1"
    w := w - (A_int[i][j] * y_int[j]);
    j := j + 1;
  }
  ---
  y_int[i] := w;
}
---

// outer 3
let i: N_bw = N - 1;
while (i >= 0 && i < N) {
  decor "#pragma HLS loop_tripcount WHILE3"
  let w: ubit<32> = y_int[i];
  // inner 3.1
  let j: N_bw = i + 1;
  while (j < N) {
    decor "#pragma HLS loop_tripcount WHILE3_1"
    w := w - (A_int[i][j] * x_int[j]);
    j := j + 1;
  }
  ---
  if (A_int[i][i] != 0) {
    x_int[i] := w / A_int[i][i];
  }
  i := i - 1;
}
