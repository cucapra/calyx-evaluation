
decl A_int: ubit<32>[N][N];

for (let i: N_bw = 0..N) {
  let j: N_bw = 0;
  while (j < i) {
    decor "#pragma HLS loop_tripcount WHILE0"
    let k: N_bw = 0;
    while(k < j) {
      decor "#pragma HLS loop_tripcount WHILE0_1"
      let A_i_j = A_int[i][j];
      ---
      let A_i_k = A_int[i][k];
      ---
      let A_k_j = A_int[k][j];
      ---
      A_int[i][j] := A_i_j - A_i_k * A_k_j;

      // Update loop counter
      k := k + 1;
    }
    ---
    let A_i_j = A_int[i][j];
    ---
    let A_j_j = A_int[j][j];
    ---
    A_int[i][j] := A_i_j / A_j_j;

    // Update loop counter
    j := j + 1;
  }
  ---
  j := i;
  while (j < N) {
    decor "#pragma HLS loop_tripcount WHILE1"
    let k: N_bw = 0;
    while (k < i) {
      decor "#pragma HLS loop_tripcount WHILE1_1"
      let A_i_j = A_int[i][j];
      ---
      let A_i_k = A_int[i][k];
      ---
      let A_k_j = A_int[k][j];
      ---
      A_int[i][j] := A_i_j - A_i_k * A_k_j;
      // Update loop counter
      k := k + 1;
    }
    // Update loop counter
    j := j + 1;
  }
}
